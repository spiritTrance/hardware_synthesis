`timescale 1ns / 1ps
`include "define_alu_ctrl.vh"

/*	port statement
 *	a, b: operator source, a is rs and b is rt in MIPS
 *	op: alu arithmetic signal
 *	y: arithmetic result
 */
module alu(
	input 	wire 			clk, rst,
	input 	wire	[31:0] 	a, b,
	input 	wire	[4:0] 	op,
	output 	reg		[31:0] 	y,
	input 	wire	[4:0]	sa,
	input 	wire 	[63:0]	HILO_i,
	input 	wire	[31:0]	cp0_i,
	input  	wire			flushE,
	output 	wire 	[63:0]	HILO_o,
	output  wire			isMulOrDivComputing,	
	output 	wire 			isMulOrDivResultOk,
	output 	wire 			overflow
	// output 	wire 			zero
	// output	wire			divByZero			// TODO: é™¤é›¶ä¾‹å¤–
    );
	// arithmetic result driver
	// è®¡ç®—ç»“æžœyçš„é©±åŠ?
	wire addOverflow, subOverflow;
	always @(*) begin
		case (op)
			// logic
			`SIG_ALU_AND:	y = a & b;
			`SIG_ALU_OR :	y = a | b;
			`SIG_ALU_XOR:	y = a ^ b;
			`SIG_ALU_NOR:	y = ~(a | b);
			`SIG_ALU_LUI:	y = {b[15:0], 16'b0};
			// shift
				// logic shift: <</>>; arithmetic shift: <<</>>>
				// NOTE THAT sys function $signed() can make it considered to be a signed number
			`SIG_ALU_SLL :	y = b << sa[4:0];
			`SIG_ALU_SRL :	y = b >> sa[4:0];
			`SIG_ALU_SRA :	y = $signed(b) >>> sa[4:0];
			`SIG_ALU_SLLV:	y = b << a[4:0];
			`SIG_ALU_SRLV:	y = b >> a[4:0];
			`SIG_ALU_SRAV:	y = $signed(b) >>> a[4:0];
			// data move	å†’é™©è§£å†³çš„éœ€è¦?
			`SIG_ALU_MFHI:	y = HILO_i[63: 32];
			`SIG_ALU_MFLO:	y = HILO_i[31: 0 ];
			// arithmetic
			`SIG_ALU_ADD  : y = $signed(a) + $signed(b);
			`SIG_ALU_ADDU : y = a + b;
			`SIG_ALU_SUB  : y = $signed(a) - $signed(b);
			`SIG_ALU_SUBU : y = a - b;
			`SIG_ALU_SLT  : y = $signed(a) < $signed(b) ? 32'b1 : 32'b0; 
			`SIG_ALU_SLTU : y = a < b ? 32'b1 : 32'b0;
			// branch and jump - al/alr
			`SIG_ALU_PC8  : y = a + 32'b1000;
			// load and store:
			`SIG_ALU_MEM  :	y = $signed(a) + $signed(b);
			// mfc0:	é’ˆå¯¹é€šç”¨å¯„å­˜å™¨å†’é™©è§£å†³çš„éœ?è¦?
			`SIG_ALU_MFC0 : y = cp0_i;
			// fail
			`SIG_ALU_FAIL:	y = 32'b0;
			default : 		y = 32'b0;
		endcase	
	end

	// ä¹˜é™¤æ³•å¤„ç?
	wire [63: 0] result_mul, result_div;
	wire isMul, isDiv, sign;
	wire isMulResultOk, isDivResultOk;
	assign isMul = (op == `SIG_ALU_MULT) | (op == `SIG_ALU_MULTU);
	assign isDiv = (op == `SIG_ALU_DIV) | (op == `SIG_ALU_DIVU);
	assign sign = (op == `SIG_ALU_DIV) | (op == `SIG_ALU_MULT);
	assign isMulOrDivResultOk = isMulResultOk | isDivResultOk;
	assign isMulOrDivComputing = (isMul | isDiv) & (~isMulOrDivResultOk); 
	mul mul_example(clk, rst, isMul, a, b, sign, 1'b0, isMulResultOk, result_mul);		// ä¹˜æ³•å™¨ä»¶
	div div_example(clk, rst, sign, a, b, isDiv & ~isDivResultOk, 1'b0, result_div, isDivResultOk);	// é™¤æ³•å™¨ä»¶

	// overflow caseï¼?
	// a[31] b[31] 1'b1, y[31] 1'b0
	// a[31] b[31] 1'b0, y[31] 1'b1
	assign addOverflow = (op == `SIG_ALU_ADD) ? // æ˜¯å¦ä¸ºaddå’ŒsubæŒ‡ä»¤
							(a[31] ^ b[31]) ? 1'b0 :	//ä¸¤ä¸ªæ“ä½œæ•°æ˜¯å¦å¼‚å·ï¼Œå¼‚å·åˆ™ä¸€å®šä¸ä¼šæº¢å‡?
								 (a[31] ^ y[31]) ? 1'b1 : 1'b0 : 1'b0;	// è¾“å…¥å’Œè¾“å‡ºçš„ç¬¦å·ä½ï¼Œå¦‚æžœå¼‚å·åˆ™å‘ç”Ÿæº¢å‡ºï¼Œå¦åˆ™æ²¡æœ‰ï¼›æœ€åŽä¸€ä¸ªæ ‡è¯†ä¸ä¸ºaddå’Œsub
	assign subOverflow = (op == `SIG_ALU_SUB) ? 
							~(a[31] ^ b[31]) ? 1'b0 :	// ä¸¤ä¸ªæ“ä½œæ•°æ˜¯å¦åŒå·ï¼ŒåŒå·åˆ™ä¸€å®šä¸æº¢å‡º
							 (a[31] ^ y[31]) ? 1'b1 : 1'b0 : 1'b0;							// ä¸¤ç§æƒ…å†µï¼šæ­£æ•°å‡è´Ÿæ•°ï¼Œè´Ÿæ•°å‡æ­£æ•°ï¼Œæ­£ç¡®ç»“æžœæ˜¯æ­£æ•°å’Œè´Ÿæ•°ï¼Œå³ç»“æžœä¸Žè¢«å‡æ•°åŒå·ï¼Œå¦‚æžœç›¸å¼‚ï¼Œå¼‚æˆ–ç¬¦å·ä½å¿…ç„¶ä¸?1
	assign overflow = addOverflow | subOverflow;
	// assign zero = (y == 32'b0);

	// HILO
	assign HILO_o = isMul & isMulResultOk ? result_mul:
				  	isDiv & isDivResultOk ? result_div:
				  	64'b0;
endmodule
